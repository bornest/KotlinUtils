buildscript {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_ver"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"

        classpath "com.github.dcendents:android-maven-gradle-plugin:$android_maven_plugin_ver"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_plugin_ver"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka'

group='com.github.bornest'

android {
    compileSdkVersion rootProject.ext.compile_sdk_ver
    buildToolsVersion "$build_tools_ver"

    defaultConfig {
        minSdkVersion rootProject.ext.minsdk_ver
        targetSdkVersion rootProject.ext.targetsdk_ver

        versionCode rootProject.ext.lib_ver_code
        versionName "$lib_ver"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {}

        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.all {
            testLogging {
                outputs.upToDateWhen {false}
                events "passed", "failed", "standardError"
                showCauses true
                showExceptions true
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //region core
        compile project(path: ':core')
    //endregion

    //region Tests

        //region Unit tests
            testCompile 'junit:junit:4.12'
            testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_ver"

            testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_ver"

            //region Mockito
                // required if you want to use Mockito for unit tests
                testCompile "org.mockito:mockito-core:$mockito_ver"

                testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_ver"
                //For compatibility with kotlin version used in the project
                testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_ver"
            //endregion

        //endregion

        //region Instrumented tests
            androidTestCompile "com.android.support:support-annotations:$support_lib_ver"
            androidTestCompile ("com.android.support.test:runner:$support_test_ver", { exclude group: 'com.android.support' })
            androidTestCompile ('com.android.support.test:rules:0.5', { exclude group: 'com.android.support' })
            androidTestCompile "org.hamcrest:hamcrest-library:$hamcrest_ver"

            androidTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_ver"
            androidTestCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_ver"

            //region Mockito
                // required if you want to use Mockito for Android tests
                androidTestCompile "org.mockito:mockito-android:$mockito_ver"

                androidTestCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_ver"
                //For compatibility with kotlin version used in the project
                androidTestCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_ver"
            //endregion

        //endregion

    //endregion


    //region Kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_ver"
    //endregion

    //region RxJava 2.x
        compile "io.reactivex.rxjava2:rxjava:$rxjava2_ver"
    //endregion

    //region Concurrent-Locks
        compile "com.googlecode.concurrent-locks:concurrent-locks:1.0.0"
    //endregion

    //region UniTimber
        compile 'com.github.bornest.UniTimber:core:-SNAPSHOT'
        testCompile 'com.github.bornest.UniTimber:test-junit:-SNAPSHOT'
        androidTestCompile 'com.github.bornest.UniTimber:test-junit:-SNAPSHOT'
    //endregion
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
